plugins {
    id 'fabric-loom' version '1.4-SNAPSHOT'
    id 'maven-publish'
    id 'idea'
}

base {
    archivesName = "${project.property("mod_name")}-${project.name}"
}

Project commonProject = parent.project("Common")

repositories {
    maven {
        url "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation commonProject

    // Modmenu
    modRuntimeOnly("maven.modrinth:modmenu:${modmenu_version}")
}

loom {
    mixin {
        defaultRefmapName = "${mod_id}.refmap.json"
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run_client")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run_server")
        }
    }
}

processResources {
    from commonProject.sourceSets.main.resources
}

tasks.withType(JavaCompile) {
    source(commonProject.sourceSets.main.allSource)
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId project.base.archivesName.get()
            from components.java
        }
    }
}
